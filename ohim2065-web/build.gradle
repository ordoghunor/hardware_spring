apply plugin: "war"

dependencies {
    implementation project(':ohim2065-backend')
    // https://mvnrepository.com/artifact/jakarta.servlet/jakarta.servlet-api
    compileOnly 'jakarta.servlet:jakarta.servlet-api:5.0.0'
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4.2'
    implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.31'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.4.2'
}

def deploymentName = 'hardware-ohim2065-web'

def tomcatHome = System.getenv('CATALINA_HOME')
logger.quiet("Tomcat home detected as '${tomcatHome}'")

// ezzel a taszkkal telepítjük ki a wart a Tomcatre
task deploy(type: Copy, dependsOn: 'war') {
    // futás előtt megnézzük, hogy a Tomcat home-ja helyesen be van-e állítva
    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }
    // a "war" task kimenetét másoljuk
    from war.archiveFile
    // átnevezzük a war állományt a megfelelő contextRoot kialakításáért
    rename war.archiveFileName.get(), "${deploymentName}.war"
    // a másolás célpontja a Tomcat "webapps" almappája
    into "${tomcatHome}/webapps"
}
// Tomcat kitelepítés törlése
task undeploy(type: Delete) {
    // futás előtt megnézzük, hogy a Tomcat home-ja helyesen be van-e állítva
    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }
    // töröljük a kitelepített war file-t
    delete "${tomcatHome}/webapps/${deploymentName}.war"
}
